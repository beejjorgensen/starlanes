10 REM THE GAME OF STARLANES - AN INTERSTELLAR COMMERCE GAME
20 REM FOR 2 TO 4 PLAYERS - COPYRIGHT 1977 BY STEVEN FARBER
30 REM WRITTEN IN ALTAIR BASIC 12/17/76
31 REM MODIFIED FOR APPLESOFT II BY RICHARD NAPOLI - 6/24/79
32 REM LISTED IN INTERFACE AGE JUNE 1977
38 CALL -936
39 HOME : VTAB (12)
40 PRINT TAB( 15);"STAR LANES"
50 PRINT : PRINT : PRINT
60 DIM M(10,13),S(5,4),N$(5),D1(5),S1(5),Q(5)
70 M$(5) = "ERIDANI EXPEDITERS": FOR I = 1 TO 5: FOR J = 1 TO 4:S(I,J) = 0
75 D1(I) = 0:S1(I) = 100:Q(I) = 0:B(I) = 6000: NEXT J,I
80 M$(3) = "CAPELLA FREIGHT CO.":M$(4) = "DENEBOLA SHIPPERS"
90 M$(1) = "APPLE STARWAYS":M$(2) = "BETELGEUSE, LTD."
100 L$ = ".+*ABCDE":M$ = "ABCDEFGHIJKL": FOR I = 1 TO 9: FOR J = 1 TO 12
110 IF INT( RND(1) *20) +1 < >10 THEN M(I,J) = 1: GOTO 130
120 M(I,J) = 3
130 NEXT J,I: HOME : INPUT "HOW MANY PLAYERS (2 - 4)";P1
140 INPUT "DOES ANY PLAYER NEED INSTRUCTIONS";R$
150 IF LEFT$(R$,I) = "Y" THEN GOSUB 8000
160 FOR I = 1 TO P1: PRINT : PRINT "PLAYER ";I;: INPUT " WHAT IS YOUR NAME ";P$(I)
170 NEXT I: PRINT : PRINT "NOW I WILL DECIDE WHO GOES FIRST...": PRINT
180 I = INT(P1 * RND(1) +1): PRINT P$(I);" IS THE FIRST PLAYER TO MOVE."
182 FOR TM = 1 TO 1000: NEXT TM
185 K = 0
190 P = I: GOTO 220
200 K = K +1: IF K = 48 THEN 9500
210 P = P +1: IF P = P1 +1 THEN P = 1
220 FOR I = 1 TO 5
230 R(I) = INT(9 * RND(1) +1):C(I) = INT(12 * RND(1) +1)
240 FOR I1 = I -1 TO 0 STEP -1: IF R(I) = R(I1) AND C(I) = C(I1) THEN 230
250 NEXT I1: IF M(R(I),C(I)) >1 THEN 230
260 FOR I1 = 1 TO 5: IF Q(I1) = 0 THEN 340
270 NEXT I1: IF M(R(I),C(I) +1) >3 OR M(R(I),C(I) -1) >3 THEN 340
280 IF M(R(I) +1,C(I)) >3 OR M(R(I) -1,C(I)) >3 THEN 340
290 A1 = M(R(I),C(I) +1):A2 = M(R(I),C(I) -1)
300 A3 = M(R(I) +1,C(I)):A4 = M(R(I) -1,C(I))
310 IF A1 = 2 AND A2 <4 AND A3 <4 AND A4 <4 THEN 230
315 IF A2 = 2 AND A1 <4 AND A3 <4 AND A4 <4 THEN 230
320 IF A3 = 2 AND A1 <4 AND A2 <4 AND A4 <4 THEN 230
325 IF A4 = 2 AND A1 <4 AND A2 <4 AND A3 <4 THEN 230
330 IF A1 = 3 AND A2 <4 AND A3 <4 AND A4 <4 THEN 230
332 IF A2 = 3 AND A1 <4 AND A3 <4 AND A4 <4 THEN 230
335 IF A3 = 3 AND A1 <4 AND A2 <4 AND A3 <4 THEN 230
337 IF A4 = 3 AND A1 <4 AND A2 <4 AND A3 <4 THEN 230
340 NEXT I: GOSUB 1000
350 HOME : PRINT : PRINT P$(P);", HERE ARE YOUR LEGAL MOVES FOR THIS TURN: "
360 FOR I = 1 TO 5: PRINT R(I); MID$ (M$,C(I),1);" /";: NEXT I: PRINT
370 INPUT "WHAT IS YOUR MOVE ";R$: IF LEFT$(R$,1) = "M" THEN GOSUB 1000: GOTO 350
372 IF LEFT$(R$,1) = "S" THEN GOSUB 1440: GOTO 350
374 IF R$ = "END" THEN GOTO 9500
375 R = VAL( LEFT$(R$,1))
380 C = ASC( RIGHT$(R$,1)) -64: FOR I = 1 TO 5: IF R = R(I) AND C = C(I) THEN 400
390 NEXT I: PRINT "THAT SPACE WAS NOT INCLUDED IN THE LIST...": GOTO 370
400 A1 = M(R -1,C):A2 = M(R +1,C):A3 = M(R,C +1):A4 = M(R,C -1)
410 IF A1 < = 1 AND A2 < = 1 AND A3 < = 1 AND A4 < = 1 THEN M(R,C) = 2: GOTO 800
420 IF A1 >3 AND A2 >3 AND A2 < >A1 THEN GOSUB 1060
430 IF A1 >3 AND A3 >3 AND A3 < >A1 THEN GOSUB 1060
440 IF A1 >3 AND A4 >3 AND A4 < >A1 THEN GOSUB 1060
450 IF A2 >3 AND A3 >3 AND A3 < >A2 THEN GOSUB 1060
460 IF A2 >3 AND A4 >3 AND A4 < >A2 THEN GOSUB 1060
470 IF A3 >3 AND A4 >3 AND A4 < >A3 THEN GOSUB 1060
480 IF A1 <4 AND A2 <4 AND A3 <4 AND A4 <4 THEN 660
490 IF M(R,C) >3 THEN 800
500 IF A1 >3 THEN I = A1 -3
510 IF A2 >3 THEN I = A2 -3
520 IF A3 >3 THEN I = A3 -3
530 IF A4 >3 THEN I = A4 -3
540 Q(I) = Q(I) +1:S1(I) = S1(I) +100:M(R,C) = I +3: GOTO 700
660 FOR I = 1 TO 5: IF Q(I) = 0 THEN 680
670 NEXT I: IF M(R,C) <3 THEN M(R,C) = 2: GOTO 800
680 GOSUB 7900: PRINT "A NEW SHIPPING COMPANY HAS BEEN FORMED!!"
690 PRINT "IT'S NAME IS ";M$(I):S(I,P) = S(I,P) +5:Q(I) = 1
695 PRINT : PRINT : PRINT : PRINT : PRINT
700 IF A1 = 3 THEN S1(I) = S1(I) +500
710 IF A2 = 3 THEN S1(I) = S1(I) +500
720 IF A3 = 3 THEN S1(I) = S1(I) +500
730 IF A4 = 3 THEN S1(I) = S1(I) +500
740 IF A1 = 2 THEN S1(I) = S1(I) +100:Q(I) = Q(I) +1:M(R -1,C) = I +3
750 IF A2 = 2 THEN S1(I) = S1(I) +100:Q(I) = Q(I) +1:M(R +1,C) = I +3
760 IF A3 = 2 THEN S1(I) = S1(I) +100:Q(I) = Q(I) +1:M(R,C +1) = I +3
770 IF A4 = 2 THEN S1(I) = S1(I) +100:Q(I) = Q(I) +1:M(R,C -1) = I +3
780 IF S1(I) >3000 THEN T1 = I: GOSUB 1400
790 M(R,C) = I +3
800 FOR I = 1 TO 5:B(P) = B(P) + INT(.05 *S(I,P) *S1(I)): NEXT I
810 FOR I = 1 TO 5: IF Q(I) = 0 THEN 900
820 PRINT "YOUR CURRENT CASH= $";B(P)
830 PRINT "BUY HOW MANY SHARES OF ";M$(I)" AT $";S1(I): PRINT TAB( 5);
840 PRINT "YOU NOW OWN ";S(I,P);
850 INPUT R3$: IF LEFT$(R3$,1) = "M" THEN R3$ = "": GOSUB 1000: GOTO 830
851 IF R3$ = "END" THEN GOTO 9500
855 IF LEFT$(R3$,1) = "S" THEN R3$ = "": GOSUB 1440: GOTO 830
856 R3 = VAL(R3$):R3$ = ""
860 IF R3 *S1(I) < = B(P) THEN 880
870 PRINT "YOU ONLY HAVE $ ";B(P);"- TRY AGAIN": GOTO 830
880 IF R3 = 0 THEN 900
890 S(I,P) = S(I,P) +R3:B(P) = B(P) -(R3 *S1(I))
900 NEXT I: GOTO 200
999 END
1000 HOME : VTAB (1): PRINT TAB( 10);"MAP OF THE GALAXY"
1010 PRINT TAB( 8);"*********************"
1020 PRINT TAB( 2)" A B C D E F G H I J K L"
1030 FOR R2 = 1 TO 9: PRINT R2;: FOR C2 = 1 TO 12: PRINT " ";
1040 PRINT MID$ (L$,M(R2,C2),1);" ";: NEXT C2: PRINT : NEXT R2
1049 POKE 34,12
1050 RETURN
1060 F1 = A1 -3: IF F1 <0 THEN F1 = 0
1061 F2 = A2 -3: IF F2 <0 THEN F2 = 0
1062 F3 = A3 -3: IF F3 <0 THEN F3 = 0
1063 F4 = A4 -3: IF F4 <0 THEN F4 = 0
1065 T = Q(F1):T1 = F1: IF Q(F2) >Q(F1) THEN T = Q(F2):T1 = F2
1070 IF Q(F3) >T THEN T = Q(F3):T1 = F3
1080 IF Q(F4) >T THEN T = Q(F4):T1 = F4
1090 IF F1 = T1 OR A1 <4 THEN 1110
1100 X = F1: GOSUB 1180
1110 IF F2 = T1 OR A2 <4 THEN 1130
1120 X = F2: GOSUB 1180
1130 IF F3 = T1 OR A3 <4 THEN 1150
1140 X = F3: GOSUB 1180
1150 IF F4 = T1 OR A4 <4 THEN 1170
1160 X = F4: GOSUB 1180
1169 FOR TM = 1 TO 1000: NEXT TM
1170 RETURN
1180 GOSUB 7900: PRINT M$(X);" HAS JUST BEEN MERGED INTO ";
1190 PRINT M$(T1);" PLEASE NOTE THE FOLLOWING TRANSACTIONS"
1200 PRINT : PRINT TAB( 4);"OLD STOCK = ";M$(X): PRINT TAB( 4);"NEW STOCK = ";M$(T1)
1210 PRINT
1220 PRINT TAB( 10);"OLD"; TAB( 18);"NEW"; TAB( 26);"TOTAL"; TAB( 34);"BONUS"
1230 PRINT "PLAYER"; TAB( 10);"STOCK"; TAB( 18);"STOCK"; TAB( 26);"SHARES"; TAB( 34);"PAID"
1240 FOR I = 1 TO P1: PRINT P$(I); TAB( 10);S(X,I); TAB( 18); INT((.5 *S(X,I)) +.5);
1250 PRINT TAB( 26);S(T1,I) + INT((.5 *S(X,I)) +.5);
1260 X1 = 0: FOR I1 = 1 TO P1:X1 = X1 +S(X,I1): NEXT I1
1265 PRINT TAB( 34);
1267 IF X1 = 0 THEN PRINT : GOTO 1275
1270 PRINT " $"; INT(10 *((S(X,I)/X1) *S1(X))):
1275 NEXT I
1290 FOR I = 1 TO P1:S(T1,I) = S(T1,I) + INT((.5 *S(X,I)) +.5)
1295 IF X1 = 0 THEN 1301
1300 B(I) = B(I) + INT(10 *((S(X,I)/X1) *S1(X)))
1301 NEXT I
1310 FOR I = 1 TO 9: FOR J = 1 TO 12: IF M(I,J) = X +3 THEN M(I,J) = T1 +3
1315 NEXT J,I
1317 A1 = M(R -1,C):A2 = M(R +1,C):A3 = M(R,C +1):A4 = M(R,C -1)
1318 F1 = A1 -3: IF F1 <0 THEN F1 = 0
1319 F2 = A2 -3: IF F2 <0 THEN F2 = 0
1320 Q(T1) = Q(T1) +Q(X):S1(T1) = S1(T1) +S1(X): IF S1(T1) >3000 THEN GOSUB 1400
1321 F3 = A3 -3: IF F3 <0 THEN F3 = 0
1322 F4 = A4 -3: IF F4 <0 THEN F4 = 0
1340 S1(X) = 100:Q(X) = 0: FOR I = 1 TO P1:S(X,I) = 0: NEXT I
1355 PRINT : PRINT : PRINT : PRINT : PRINT
1360 M(R,C) = T1 +3
1370 RETURN
1400 GOSUB 7900: PRINT "THE STOCK OF ";
1410 PRINT M$(T1);" HAS SPLIT 2 FOR 1!":S1(T1) = INT(S1(T1)/2)
1415 PRINT : PRINT : PRINT : PRINT : PRINT
1420 FOR I1 = 1 TO P1:S(T1,I1) = 2 *S(T1,I1): NEXT I1
1425 FOR TM = 1 TO 1000: NEXT TM
1430 RETURN
1440 PRINT
1450 PRINT "STOCK"; TAB( 18);"PRICE/SHARE"; TAB( 30);"HOLDINGS"
1460 FOR I3 = 1 TO 5: IF S1(I3) = 100 THEN 1480
1470 PRINT M$(I3); TAB( 18);S1(I3); TAB( 30);S(I3,P)
1480 NEXT I3:
1481 FOR TM = 1 TO 1000: NEXT TM
1482 RETURN
7900 TEXT : PRINT ""
7910 PRINT TAB( 10);"SPECIAL ANNOUNCEMENT"
7920 PRINT : RETURN
8000 HOME : VTAB (1): INVERSE : PRINT "HIT ANY KEY TO CONTINUE INSTRUCTIONS ": NORMAL : PRINT : POKE 34,2
8001 HOME : GET R$
8010 PRINT "STAR LANES IS A GAME OF INTERSTELLAR TRADING. THE OBJECT OF THE GAME IS TO AMASS THE GREATEST AMOUNT OF MONEY. THIS IS ACCOMPLISHED BY ESTABLISHING "
8020 PRINT "VAST, INTERSTELLAR SHIPPING LANES,AND PURCHASING STOCK IN THE COMPANIES THAT CONTROL THESE TRADE ROUTES. DURING THE COURSE OF THE GAME, STOCK APPRECIATES IN"
8030 PRINT "VALUE AS THE SHIPPING COMPANIES BECOME LARGER. ALSO, SMALLER COMPANIES CAN BE MERGED INTO LARGER ONES, AND STOCK IN THE SMALLER FIRM IS CONVERTED INTO STOCKIN THE LARGER ONE"
8049 PRINT
8050 PRINT "EACH TURN, THE COMPUTER WILL PRESENT THEPLAYER WITH FIVE PROSPECTIVE SPACES TO OCCUPY ON A 9 X 12 MATRIX (ROWS 1-9, COLUMNS A-L). THE PLAYER, AFTER EXAMIN-ING THE MAP OF THE GALAXY TO DECIDE "
8055 PRINT : GET R$
8060 PRINT "WHICH SPACE HE WISHES TO OCCUPY, RE- SPONDS WITH THE ROW AND COLUMN OF THAT SPACE; 1E, 7E, 8A, ETC."
8070 PRINT
8080 PRINT "THERE ARE FOUR POSSIBLE MOVE TO MAKE"
8085 GET R$: HOME
8090 PRINT "1). HE CAN ESTABLISH AN UNATTACHED OUT- POST -- IF HE SELECTS A SPACE THAT IS NOT ADJACENT TO A STAR, ANOTHER UN- ATTACHED OUTPOST, OR AN EXISTING SHIP- ING LANE,THIS SPACE WILL BE DESIGNATED"
8100 PRINT "WITH A '+'. HE WILL THEN PROCEDE WITH STOCK TRANSACTIONS."
8110 GET R$: HOME
8120 PRINT "2). HE CAN ADD TO AN EXISTING LANE. IF HE SELECTS A SPACE WHICH IS ADJACENT TO ONE -- AND ONLY ONE EXISTING SHIPPING LANE, THE SPACE HE SELECTS WILL BE ADDEDTO THAT SHIPPING LANE , AND WILL BE DE-"
8130 PRINT "SIGNATED WITH THE FIRST LETTER OF THE COMPANY THAT OWNS THAT LANE. IF THERE ARE ANY STARS OR UNATTACHED OUTPOSTS ALSO ADJACENT TO THE SELECTED SPACE THEY, TOO, WILL BE INCORPORATED INTO THE"
8140 PRINT "EXISTING LANE. EACH NEW SQUARE ADJACENTTO A STAR ADDS $500 PER SHARE, AND EACH NEW OUTPOST ADDS $100 PER SHARE TO THE MARKET VALUE OF THAT SHIPPING COMPANY"
8150 GET R$: HOME : PRINT "3)."
8160 PRINT "HE MAY ESTABLISH A NEW SHIPPING LANE. IFTHERE ARE LESS THAN FIVE EXISTING SHIP- PING LANES ESTABLISHED, THE PLAYER MAY, GIVEN THE PROPER SPACE TO PLAY, ESTAB- LISH A NEW SHIPPING LANE. HE MAY DO"
8170 PRINT "THIS BY OCCUPYING A SPACE ADJACENT TO A STAR OR ANOTHER UNATTACHED OUTPOST BUT NOT ADJACENT TO AN EXISTING SHIPPING LANE. IF HE ESTABLISHES A NEW SHIPPING LANE, HE IS AUTOMATICALLY ISSUED FIVE"
8180 PRINT "SHARES IN THE NEW COMPANY AS A REWARD. HE MAY THEN PROCEDE TO BUY STOCK IN THATCOMPANY, OR ANY OTHER ACTIVE COMPANY, ASDESCRIBED BELOW. THE MARKET VALUE OF THE NEW STOCK IS ESTABLISHED BY THE"
8190 PRINT "NUMBER OF STARS AND OCCUPIED SPACES AS DESCRIBED IN 2) ABOVE."
8200 GET R$: HOME
8210 PRINT "4). HE MAY MERGE TWO EXISTING COMPANIES.IF THE PLAYER SELECTS A SPACE ADJACENT TO TWO EXISTING SHIPPING LANES, A MERGEROCCURS. THE LARGER COMPANY TAKES OVER THE SMALLER COMPANY; THE STOCK OF THE"
8220 PRINT "LARGER COMPANY IS INCREASES IN VALUE ACCORDING TO THE NUMBER OF SPACES AND STARS ADDED TO ITS LANE. EACH PLAYER'S STOCK IN THE SMALLER COMPANY IS EXCHANG-ED FOR SHARES IN THE LARGER ON A RATIO"
8230 PRINT "OF 2 SHARES OF THE SMALLER = 1 SHARE OF THE LARGER. ALSO, EACH PLAYER IS PAID A CASH BONUS PROPORTIONAL TO THE PERC- ENTAGE OF THE OUTSTANDING STOCK HE HELD IN THE SMALLER COMPANY.NOTE: AFTER A "
8240 PRINT "COMPANY BECOMES DEFUNCT THROUGH THIS MERGER PROCESS IT CAN REAPPEAR ELSE- WHERE ON THE BOARD IF A NEW COMPANY IS ESTABLISHED.(SEE #3 ABOVE)"
8250 GET R$: HOME
8260 PRINT "NEXT THE COMPUTER ADDS THE STOCK DIVI- DENDS TO THE PLAYER'S CASH ON HAND (5% OF THE MARKET VALUE OF THE STOCK IN HIS POSSESSION), AND OFFERS HIM THE OPPORTUNITY TO PURCHASE STOCK IN ANY"
8270 PRINT "OF THE ACTIVE COMPANIES ON THE BOARD. STOCK MAY NOT BE SOLD, BUT THE MARKET VALUE OF THE STOCK IS TAKEN INTO ACCOUNTAT THE END OF THE GAME TO DETERMINE THE WINNER. IF THE MARKET VALUE OF A GIVEN
8280 PRINT "STOCK EXCEEDS $3000 AT ANY TIME DURING THE GAME, THAT STOCK SPLITS TWO FOR ONE.THE PRICE IS CUT IN HALF AND THE NUMBER OF SHARES OWNED BY EACH PLAYER IS"
8290 PRINT "DOUBLED"
8295 GET R$: HOME
8297 PRINT "NOTE: THE PLAYER MAY LOOK AT HIS PORT- FOLIO AT ANY TIME DURING THE COURSE OF HIS TURN BY RESPONDIND TO AN INPUT RE- QUEST WITH 'STOCK'. LIKE WISE HE CAN"
8300 PRINT "REVIEW THE MAP OF THE GALAXY BY TYPING 'MAP' TO AN INPUT REQUEST. THE GAME ENDS AFTER 48 MOVES. THE GAME MAY BE TERMINATED BEFORE THE 48 MOVE BY TYP- ING 'END' TO AN INPUT REQUEST"
8305 GET R$: TEXT : HOME
8310 PRINT : PRINT "TO REVIEW THE INSTRUCTIONS AGAIN, TYPE 'R'. TO BEGIN PLAY, TYPE 'P'"
8320 GET R$: IF R$ = "R" THEN 8000
8330 IF R$ = "P" THEN 8400
8335 GOTO 8320
8400 HOME : RETURN
9000 END
9500 GOSUB 7900: PRINT TAB( 13);"THE GAME IS OVER": PRINT TAB( 7);"HERE ARE THE FINAL STANDINGS"
9505 PRINT
9510 PRINT "PLAYER"; TAB( 10);"VALUE/STOCK"; TAB( 22);"CASH"; TAB( 30);"NET WORTH": PRINT
9530 FOR I = 1 TO P1: FOR J = 1 TO 5:D1(I) = D1(I) +(S1(J) *S(J,I)): NEXT J,I
9540 FOR I = 1 TO P1: PRINT P$(I); TAB( 10);"$";D1(I); TAB( 22);"$";B(I);
9550 PRINT TAB( 30);"$";D1(I) +B(I): NEXT I
9560 PRINT "ANOTHER GAME?": GET R$: IF R$ = "Y" THEN 70
